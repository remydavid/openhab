rule "Mobile presence"
when
  Time cron "* * * * * ?"
then
  if (presence.state == ON)
  {
    if(gMobiles.members.filter(s | s.state == ON).size == 0) {
      logInfo("PresenceCheck", "No phone within reach, checking for flapping")
      if(gMobiles.members.filter(s | s.changedSince(now.minusMinutes(5))).size == 0) {
        logInfo("PresenceCheck", "Nobody is at home")
        sendCommand(presence, OFF)
      }
    }
  }
  else
  {
    //For initialisation. If Presence is undefined or off, although it should be on.
    if(gMobiles.members.filter(s | s.state == ON).size > 0) {
      sendCommand(presence, ON)
    }
    else if (presence.state == Undefined || presence.state == Uninitialized) {
      sendCommand(presence, OFF)
    }
  }
end

rule "Sensor"
when
  Item motion_1_detect changed from OFF to ON
then
  if (presence.state != ON) {
    logInfo("PresenceCheck", "Somebody is home")
    sendCommand(presence, ON)
  }
end

rule "Coming home"
when
  Item gMobiles changed
then
  if (presence.state != ON) {
    if(gMobiles.members.filter(s | s.state == ON).size > 0) {
      logInfo("PresenceCheck", "Somebody is home")
      sendCommand(presence, ON)
    }
  }
end
